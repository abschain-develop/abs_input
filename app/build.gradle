apply plugin: 'com.android.application'

repositories {
    flatDir {
        dirs './libs'
    }
}

ext { // 定义当前gradle文件的全局变量
    isRelease = checkRelease()
    targetTask = ""
}

// 通过taskName判断当前操作是否是在打release包，只需执行一次
def checkRelease() {
    def runTasks = gradle.startParameter.taskNames
    for (String task : runTasks) {
        // 我这里认为执行“assemble”和非“debug”的任务就是执行“release”的任务
        if (task.contains("assemble") && !task.contains("Debug")) {
            ext.targetTask = task
            return true
        }
    }
    return false
}

def increasedVersionCode() {
    def propFile = file('../gradle.properties')
    def Properties props = new Properties()
    props.load(new FileInputStream(propFile))
    // 读取gradle.properties文件中定义的VERSION_CODE属性
    def code = props['VERSION_CODE'].toInteger()
    if (isRelease) {
        code++
        props['VERSION_CODE'] = code.toString()
        // 将最新的versionCode写入gradle.properties文件中
        props.store(propFile.newWriter(), null)
    }
    return code
}

android {
    compileSdkVersion 26

    aaptOptions {
        noCompress 'dat'
    }
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        applicationId "com.abs.inputmethod.pinyin"
        versionCode increasedVersionCode()
        versionName "1.1.0"

        externalNativeBuild {
            cmake {
                cppFlags "-Wall"
            }
        }

        ndk {
            abiFilters 'armeabi-v7a'
        }

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation(name: 'ipks-arm-v1.0.0-190815', ext: 'aar')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'org.web3j:core:3.3.1-android'
    implementation 'io.github.novacrypto:BIP39:0.1.9'//用于生成助记词
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.github.promeg:tinypinyin:2.0.3' // TinyPinyin核心包，约80KB
    implementation 'com.github.promeg:tinypinyin-lexicons-android-cncity:2.0.3' // 可选，适用于Android的中国地区词典
    implementation 'com.google.code.gson:gson:2.8.5'
}
